<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en-us">
<head>

   <!-- Standard site meta information -->
   <meta http-equiv="content-type" content="text/html; charset=utf-8" />
   <title>Minilight in Clojure: Triangles ← Inductio Ex Machina ← Mark Reid</title>
   <meta name="author" content="Mark Reid" />

   <link rel="openid.server" href="http://www.myopenid.com/server" />
   <link rel="openid.delegate" href="http://mark.reid.name" />

   <link rel="start" href="../" />

   <!-- syntax highlighting CSS -->
   <link rel="stylesheet" href="../css/syntax.css" type="text/css" />

   <!-- Homepage CSS -->
   <link rel="stylesheet" href="../css/screen.css" type="text/css" media="all" />
   <link rel="stylesheet" href="../css/print.css" type="text/css" media="print" />
   <link rel="stylesheet" href="../css/widgets.css" type="text/css" media="all" />
   <link rel="stylesheet" href="../css/amjr.css" type="text/css" media="all" />

 	<meta name="twitter:card" content="summary">
    <meta name="twitter:creator" content="@mdreid">
    <meta name="twitter:site" content="@mdreid">
    <meta name="twitter:title" content="Minilight in Clojure: Triangles">
    <meta name="twitter:description" content="This is the second part of my attempt to port the Minilight ray-tracer to Clojure. This time it is triangles. Some bugs are found and fixed in the vector package.">

<!-- MathJax configuration and loading -->
<script type="text/x-mathjax-config">
	MathJax.Hub.Config({
		extensions: ["tex2jax.js"],
		jax: ["input/TeX", "output/HTML-CSS"],
		tex2jax: {
			inlineMath: [ ["\\(","\\)"] ],
			displayMath: [ ["\\[","\\]"] ],
			processEscapes: true
		},
		TeX: { equationNumbers: { autoNumber: "AMS" } },
		"HTML-CSS": { availableFonts: ["TeX"] }
	});
</script>
<script type="text/javascript" src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML"> </script>

</head>

<body id="Blog">

<div id="site">
  <div id="header">
<h1>
	<a href="../blog/" title="A machine learning blog">Inductio ex Machina</a>
	<span class="byline">← <a href="../">Mark Reid</a></span>
</h1>
<ul class="nav">
  <li><a class="home" href="../blog/">Home</a></li>
  <li><a class="info" href="../blog/info.html">Info</a></li>
  <li><a class="past" href="../blog/past.html">Past</a></li>
  <li><a class="kith" href="../blog/kith.html">Kith</a></li>
</ul>
</div>

<div id="page">

<h1 class="emphnext">Minilight in Clojure: Triangles</h1>

<p>Previously on “Porting Minilight to Clojure”, our intrepid programmer (a.k.a, yours truly) braved lists, cross products and testing suites to bring you a namespace full of <a href="../sap/minilight-clojure-vectors.html">vector</a> operations. In this, the nail-biting sequel, I use these to build up some simple geometry in the form of triangles.</p>
<h2 id="house-keeping">House-keeping…</h2>
<p>Thanks to some <a href="http://groups.google.com/group/clojure/browse_thread/thread/3f8283421219fef5/10de2f48fb48f6b8">feedback</a> from the Clojure mailing list, I’ve made a couple of small changes to the way I’ve set up this project:</p>
<ul>
<li><p>I now use of fully qualified names. That is, instead of a package called <code>vec</code>, I will use the longer, Java-style <code>mreid.minilight.vec</code> to avoid potential name collisions.</p></li>
<li><p>The code now lives under a <code>src</code> directory in folders corresponding to their fully-qualified package name. This meant adding a <code>.clojure</code> file with the text <code>src</code> in it so that my <code>clj</code> script can find the code.</p></li>
<li><p>The tests now define a class and are executed via a main method. This is so that IDE users won’t have the tests run every time the <code>test.clj</code> file is compiled.</p></li>
</ul>
<p>Tests are now run from a simple shell script <code>runtests.sh</code> which has the following contents:</p>
<pre class="bash"><code>#!/bin/bash
JARS=$HOME/Library/Clojure/lib
CP=.:./src:$JARS/clojure.jar:$JARS/clojure-contrib.jar
TEST=&quot;src/mreid/minilight/test/all.clj&quot;
MAIN=&quot;(mreid.minilight.test.all/-main)&quot;

java -cp $CP clojure.main -i $TEST -e &quot;$MAIN&quot;</code></pre>
<h2 id="and-a-bug-fix">…and a Bug-Fix</h2>
<p>In the process of writing some new code I found a bug in my cross product implementation. After a bit of head-scratching, I realised that the form I use to extract a particular component from a vector assumes that the underlying data structure is a Clojure vector. I was using this <code>(v 0)</code> to get at the x-coordinate of the vector <code>v</code> which works fine for <code>([1 2 3] 1)</code> but fails spectacularly when the vector is computed as a lazy sequence. For example:</p>
<pre><code>Clojure
user=&gt; ((map - [1 2 3] [1 1 1]) 1)
java.lang.ClassCastException: clojure.lang.LazySeq cannot be cast to
clojure.lang.IFn (NO_SOURCE_FILE:0)</code></pre>
<p>I used <code>map</code> in several places to compute addition, subtraction and scaling of vectors so when these were passed into <code>cross</code> they blew up.</p>
<p>I’ve added some new tests to <code>test/vec.clj</code> that test for this situation explicitly:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/test/vec.clj ---
(def dyn100 (sub [1 2 3] [0 2 3]))
(def dyn010 (sub [1 2 3] [1 1 3]))

(deftest test-dynamic-add
  (is (= [1 1 0]  (add dyn100 dyn010))))

(deftest test-dynamic-sub
  (is (= [1 -1 0] (sub dyn100 dyn010))))

(deftest test-dynamic-scale
  (is (= [2 0 0]    (scale 2 dyn100))))

(deftest test-dynamic-cross
  (is (= [0 0 1]   (cross dyn100 dyn010))))</code></pre>
<p>and changed the definition of <code>cross</code> to use the sequence friendly <code>nth</code> instead:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/vec.clj ---
(defn cross 
    &quot;Returns the cross product vector for the 3D vectors v1 and v2.&quot;
    [v1 v2] 
    [ (- (* (nth v1 1) (nth v2 2)) (* (nth v1 2) (nth v2 1)))
      (- (* (nth v1 2) (nth v2 0)) (* (nth v1 0) (nth v2 2)))
      (- (* (nth v1 0) (nth v2 1)) (* (nth v1 1) (nth v2 0))) ])</code></pre>
<p>I guess this is one of those instances where static typing would have helped me realise my mistake earlier.</p>
<h2 id="triangles">Triangles</h2>
<p>Triangles are the simplest possible polygon. They are defined by three vectors, one for each vertex. As we will be using triangles to model surfaces in a ray-tracer, I will also need two other vectors to define how light is reflected and emitted from a surface. In Clojure, this is neatly captured using a <code>struct</code>:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/triangle.clj ---
;; A structure and functions for defining and querying triangles.
(ns mreid.minilight.triangle
  (:use mreid.minilight.vec))
 
(defstruct triangle
  :vertices ; Collection of 3 vectors
  :reflect  ; Vector with all values in [0,1)
  :emit     ; Vector with positive values
)</code></pre>
<p>To interrogate the structure and calculate related quantities I will also need a number of simple functions:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/triangle.clj ---
(defn vertex
  &quot;Returns (the zero-indexed) vertex i in the triangle t&quot;
  [t i] (nth (:vertices t) i))

(defn edge
  &quot;Returns the edge in the triangle t from vertex i to vertex j&quot;
  [t i j] (sub (vertex t j) (vertex t i)))

(defn tangent
  &quot;Returns a unit vector tangent to the given triangle t&quot;
  [t] (normalise (edge t 0 1)))

(defn normal
  &quot;Returns a vector normal to the given triangle t (edge01 x edge12)&quot;
  [t] (cross (edge t 0 1) (edge t 1 2)))

(defn unit-normal
  &quot;Returns a unit vector normal to the given triangle t&quot;
  [t] (normalise (normal t)))

(defn area
  &quot;Returns the are of the given triangle t&quot;
  [t] (/ (norm (normal t)) 2))</code></pre>
<p>This is all very straight-forward and, once again, Clojure just “gets out of the way” and make it very easy to express each of these simple functions on a single line and with a minimum of fuss.</p>
<p>Unit tests for these can be found in the file <code>test/triangle.clj</code>. This consists of a few simple triangle structures and then a number of tests on them:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/test/triangle.clj ---
;; Tests for triangle.clj using the test-is library.
(ns mreid.minilight.test.triangle
  (:use mreid.minilight.triangle)
  (:use clojure.contrib.test-is))

(def xytriangle
  (struct triangle 
     [ [0 0 0] [1 0 0] [0 1 0] ] ; Triangle in xy-plane
     [0.5 0.5 0.5]               ; Reflectivity
     [1 1 1]                     ; Emitivity 
  ))

(def y2ztriangle
  (struct triangle 
     [ [0 0 0] [0 2 0] [0 0 1] ] ; Triangle in xy-plane
     [0.5 0.5 0.5]               ; Reflectivity
     [1 1 1]                     ; Emitivity 
  ))

(def zxtriangle
  (struct triangle 
     [ [-10 5 -10] [-9 5 -10] [-10 5 -9] ] ; Triangle parallel to zx-plane
     [0.5 0.5 0.5]                         ; Reflectivity
     [1 1 1]                               ; Emitivity 
  ))

(deftest test-vertex
  (is (= [0 0 0] (vertex xytriangle 0)))
  (is (= [1 0 0] (vertex xytriangle 1)))
  (is (= [0 1 0] (vertex xytriangle 2)))

  (is (= [0 0 0] (vertex y2ztriangle 0)))
  (is (= [0 2 0] (vertex y2ztriangle 1)))
  (is (= [0 0 1] (vertex y2ztriangle 2))))

(deftest test-edge
  (is (= [1 0 0] (edge xytriangle 0 1)))
  (is (= [0 1 0] (edge xytriangle 0 2)))
  (is (= [-1 1 0] (edge xytriangle 1 2)))
  (is (= [1 -1 0] (edge xytriangle 2 1)))

  (is (= [0 2 0] (edge y2ztriangle 0 1)))
  (is (= [0 0 1] (edge y2ztriangle 0 2)))
  (is (= [0 -2 1] (edge y2ztriangle 1 2)))
  (is (= [0 2 -1] (edge y2ztriangle 2 1))))

(deftest test-tangent
  (is (= [1 0 0] (tangent xytriangle)))
  (is (= [0 1 0] (tangent y2ztriangle))))

(deftest test-normal
  (is (= [0 0 1] (normal xytriangle)))
  (is (= [2 0 0] (normal y2ztriangle))))

(deftest test-unit-normal
  (is (= [0 0 1] (unit-normal xytriangle)))
  (is (= [1 0 0] (unit-normal y2ztriangle))))

(deftest test-area
  (is (= 0.5 (area xytriangle)))
  (is (= 1   (area y2ztriangle))))</code></pre>
<p>There are three main functions left to port: one that computes a bounding-box for a triangle, one that tests whether a vector intersects a triangle, and one that randomly samples a point from a given triangle.</p>
<h2 id="bounding-boxes">Bounding Boxes</h2>
<p>The following function creates the smallest box containing all the points belonging to a triangle and then slightly expands it. It is pretty straight-forward, the only slightly tricky part is the higher-order function <code>tweak</code>. This takes in either a <code>+</code> or a <code>-</code> and will return a function that expands or shrinks <em>its</em> input by the specified <code>TOLERANCE</code>. This is used by <code>bounding-box</code> to pull the point of a triangle closest to the origin about 0.1% closer and push the furthest point away by 0.1%.</p>
<p>The reason for this function is that without it I would have have two copies of the code inside the function returned by <code>tweak</code> with only a sign change. I figured this was a more <a href="http://en.wikipedia.org/wiki/Don't_repeat_yourself">DRY</a> was of writing it.</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/triangle.clj ---
(def TOLERANCE (/ 1.0 1024.0))
(defn tweak
  &quot;Returns a function that adds or subtracts a small amount&quot;
  [add-or-sub]
  (fn [x] (add-or-sub x (* (+ (Math/abs x) 1.0) TOLERANCE))))

(defn bounding-box
  &quot;Computes the bounding box for a triangle t, returning the result as
  a list of two vectors [lower-corner upper-corner].&quot; 
  [t]
  (let [vs (:vertices t)]
    [ (map (tweak -) (apply map min vs))
      (map (tweak +) (apply map max vs)) ]))</code></pre>
<p>The above code is definitely not the most efficient way of computing and expanding a bounding-box. Indeed, the Ruby code that I’m cribbing this from explicitly optimises this using a single loop through each of a triangle’s vectors. In the interests of simplicity, I’ve opted for more two sweeps through each vector—once for the <code>min</code>/<code>max</code> and once for the <code>tweak</code>.</p>
<p>When testing these functions, I’ve opted for hand-building the correct values as constants defined in terms of the tolerance and then testing the output of <code>tweak</code> and <code>bounding-box</code> against these:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/test/triangle.clj ---
(def tweak+0 (* 1 TOLERANCE))
(def tweak-0 (* -1 TOLERANCE))
(def tweak+1 (+ 1 (* 2 TOLERANCE)))
(def tweak-1 (- 1 (* 2 TOLERANCE)))
(def tweak+2 (+ 2 (* 3 TOLERANCE)))
(def tweak-2 (- 2 (* 3 TOLERANCE)))

(deftest test-tweak
  (is (= tweak+0 ((tweak +) 0)))
  (is (= tweak-0 ((tweak -) 0)))
  (is (= tweak+1 ((tweak +) 1)))
  (is (= tweak-1 ((tweak -) 1)))
  (is (= tweak+2 ((tweak +) 2)))
  (is (= tweak-2 ((tweak -) 2))))

(deftest test-bounding-box
  (is (= [[tweak-0 tweak-0 tweak-0] [tweak+0 tweak+2 tweak+1]] 
         (bounding-box y2ztriangle))))</code></pre>
<h2 id="apply-liberally">Apply Liberally</h2>
<p>One discovery I made while attempting to write this was the <code>apply</code> method. Its (very terse) documentation says</p>
<pre><code>clojure.core/apply
([f args* argseq])
  Applies fn f to the argument list formed by prepending args to argseq.</code></pre>
<p>In English, what it really does is treat a sequence of items as though they were passed in as arguments to the given function. In my case, I wanted to compute the point-wise minimum of three vectors. If these were, say, <code>[1 2 3]</code>, <code>[0 5 5]</code> and <code>[1 1 1]</code> I would simply <code>map</code> <code>min</code> onto them as arguments:</p>
<pre><code>Clojure
user=&gt; (map min [1 2 3] [0 5 5] [1 1 1])
(0 1 1)</code></pre>
<p>This works because <code>min</code> can take an arbitrary number of arguments (<em>e.g.</em>, <code>min 1 0 1</code> gives 0) and <code>map</code>, when given several sequences, will pull items from each in parallel and pass them to the function it is mapping.</p>
<p>However, the problem I faced here was that the three vectors were inside a sequence and so naïvely trying the following gives the wrong result:</p>
<pre><code>user=&gt; (map min [ [1 2 3] [0 5 5] [1 1 1] ])
([1 2 3] [0 5 5] [1 1 1])</code></pre>
<p>This is where <code>apply</code> comes in. It strips away the container and presents its contents to the function being applied as arguments, along with any extra arguments before the sequence:</p>
<pre><code>user=&gt; (apply map min [ [1 2 3] [0 5 5] [1 1 1] ])
(0 1 1)</code></pre>
<h2 id="intersection">Intersection</h2>
<p>Much of the main loop of a ray-tracing algorithm involves checking whether a ray will hit a surface. Minilight is no exception, so we have the following method for testing whether a ray, represented as a starting point and a direction vector, will intersect with a given triangle:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/triangle.clj ---
(defn intersect
  &quot;Finds the intersection with the triangle t of the ray starting at r0 in 
  direction rd. The returned value is a positive number a such that r0 + a.rd 
  is contained within t, or nil if there is no such a.&quot;
  [t r0 rd]
  (let [ e01    (edge t 0 1)
         e02    (edge t 0 2)
         p      (cross rd e02)
         invdet (invdet e01 rd e02) ]
    (if (number? invdet)
      (let [ v0 (vertex t 0)
             tv (sub r0 v0)
             u  (* (dot tv p) invdet) ]
        (if (and (&gt;= u 0) (&lt;= u 1))
          (let [ q (cross tv e01)
                 v (* (dot rd q) invdet) ]
            (if (and (&gt;= v 0) (&lt;= (+ u v) 1))
              (let [a (* (dot e02 q) invdet)]
                (if (&gt;= a 0) a)))))))))</code></pre>
<p>I’m least happy with the style of this code. The repeatedly nested <code>let</code>/<code>if</code> blocks seem ugly but I cannot see a more elegant way to do this sort of thing. Partly, it’s because the series of conditional tests are essential to what is being computed, but I can’t help but feel there is a lot of what Brook’s calls “<a href="http://en.wikipedia.org/wiki/Accidental_complexity">accidental complexity</a>” in there as well.</p>
<p>Suggestions on how to improve this function are very welcome.</p>
<p>We can, at least, test that it is working though. Here I check whether hand-picked rays intersect on of the test triangles or not:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/test/triangle.clj ---
(deftest test-intersect
  (is (= 1 (intersect xytriangle [0 0 1] [0 0 -1])))
  (is (= 2 (intersect xytriangle [0 0 2] [0 0 -1])))
  (is (= 1 (intersect xytriangle [0.9 0 1] [0 0 -1])))
  (is (= 1 (intersect xytriangle [0.1 0.1 -1] [0 0 1]))))

(deftest test-no-intersect
  (is (nil? (intersect xytriangle [0 0 1] [0 0 1]))) ; Dir. is opposite
  (is (nil? (intersect xytriangle [0 0 1] [1 0 0]))) ; Dir. is parallel
  (is (nil? (intersect xytriangle [0 0 2] [0 1 -1])))) ; Goes wide</code></pre>
<h2 id="sampling">Sampling</h2>
<p>Finally, the renderer needs a way of choosing a random point uniformly from a given triangle. I’ve lifted the formula for choosing the point at random directly from the Ruby code and translated as directly as possible into Clojure.</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/triangle.clj ---
(def rnd (java.util.Random.))
(defn sample-point
  &quot;Returns a random point as a vector from inside the given triangle t&quot;
  [t]
  (let [ sqr1 (Math/sqrt (.nextFloat rnd))
         r2   (.nextFloat rnd)
         a    (- 1 sqr1)
         b    (* (- 1 r2) sqr1) ]
    (add (vertex t 0)
        (add (scale a (edge t 0 1)) 
             (scale b (edge t 0 2))))))</code></pre>
<p>By its very nature, <code>sample-point</code> is non-deterministic. This makes testing a little unusual. Rather than testing whether a specific function call returns a specific value, I’ve opted for testing an invariant. In particular, if I sample a point from a triangle and translate it one unit along its normal then a ray starting at that point which points back along the normal should intersect the original triangle:</p>
<pre class="clojure"><code>;; --- src/mreid/minilight/test/triangle.clj ---
(defn random-ray
  &quot;Returns [r0 rd] where rd is the unit normal of the triangle t and r0 is a 
   random point on t translated by -rd&quot; 
  [t]
  (let [ray-direction (unit-normal t)]
    [ (sub (sample-point t) ray-direction) 
      ray-direction ]))

(deftest test-sample-point
  (let [xy-random-ray (random-ray xytriangle)
        zx-random-ray (random-ray zxtriangle)]
    (is (= 1 (apply intersect xytriangle xy-random-ray)))
    (is (= 1 (apply intersect zxtriangle zx-random-ray)))))</code></pre>
<p>Running this test on different occasions will check whether the invariant holds for different points. A down-side is that this test is not strictly repeatable and may fail very rarely. This may make tracking down a failing test difficult (even though the failure output will offer clues). The upside, however, is that the coverage for the <code>intersect</code> function is increased.</p>
<h2 id="summary">Summary</h2>
<p>I’m definitely feeling more confident with Clojure even though I cannot instinctively count parentheses yet. Structures in Clojure feel very natural and are easy to work with and many of the functions I needed can still be expressed concisely. That said, the more mathematically involved functions didn’t lend themselves to Clojure’s prefix notation.</p>
<p>I suspect I’ll find these easier to read as time goes on but it is one case where simplicity of S-expressions falls short. Perhaps there are common ways of breaking up or otherwise formatting formulae that makes them easier to work with but they are far from obvious to me.</p>
<p>As usual, the code is on GitHub, this time tagged with <a href="http://github.com/mreid/minilight-clojure/tree/sap-2.1">sap–2.1</a>. See you there!</p>
<h2 id="updates">Updates</h2>
<ul>
<li><em>15 April 2009</em>: Fixed a bug spotted by HXA7241 in <code>sample-point</code> as well as a related bug in <code>intersect</code>. Updated the relevant tests to reproduce the bug.</li>
</ul>


<address class="signature">
	<a class="author" href="http://mark.reid.name">Mark Reid</a>
  <span class="date">April 11, 2009</span>
  <span class="location">Canberra, Australia</span>
</address>

</div>
<address id="feed" class="quiet right">Subscribe: <a href="../blog/atom.xml" title="Subscribe to Atom feed"><em>Atom Feed</em></a></address>


<!-- Disqus Comments -->
<div id="disqus_thread"></div>

<!-- Enable Disqus comments -->
<script type="text/javascript">
        var disqus_iframe_css = "http://mark.reid.name/css/screen.css";
        var disqus_title = "Minilight in Clojure: Triangles";
        var disqus_message = "This is the second part of my attempt to port the Minilight ray-tracer to Clojure. This time it is triangles. Some bugs are found and fixed in the vector package.";
</script>
<script type="text/javascript" src="http://disqus.com/forums/markreid/embed.js"></script>

<noscript>
    <a href="http://markreid.disqus.com/?url=ref">View the discussion thread.</a>
</noscript>




    <!-- Footer with copyright and contact information -->
  <div id="footer">
	<address>
		<span class="copyright">
			Content &amp; Design by 
			<a href="../info/site.html">Mark Reid</a>
			<br />
			(<a rel="licence" href="http://creativecommons.org/licenses/by-nc-sa/3.0/">Some rights reserved</a>)			
		</span>
		<span class="engine">
			Powered by 
			<a href="http://jaspervdj.be/hakyll/" title="A static, minimalist, Haskell-powered CMS">Hakyll</a>
		</span>
	</address>
  </div>

</div>


<!-- Google Analytics script -->
<script type="text/javascript">
var gaJsHost = (("https:" == document.location.protocol) ? "https://ssl." : "http://www.");
document.write(unescape("%3Cscript src='" + gaJsHost + "google-analytics.com/ga.js' type='text/javascript'%3E%3C/script%3E"));
</script>
<script type="text/javascript">
var pageTracker = _gat._getTracker("UA-1051817-4");
pageTracker._trackPageview();
</script>
<!--[if IE 6]>
<script type="text/javascript"> 
	/*Load jQuery if not already loaded*/ if(typeof jQuery == 'undefined'){ document.write("<script type=\"text/javascript\"   src=\"http://ajax.googleapis.com/ajax/libs/jquery/1.3.2/jquery.min.js\"></"+"script>"); var __noconflict = true; } 
	var IE6UPDATE_OPTIONS = {
		icons_path: "http://static.ie6update.com/hosted/ie6update/images/"
	}
</script>
<script type="text/javascript" src="http://static.ie6update.com/hosted/ie6update/ie6update.js"></script>
<![endif]-->


</body>
</html>



